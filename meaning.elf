meaning : tm -> env -> sem -> type. 
%mode meaning +M +R -E.

- : meaning unit R 1.
- : meaning ax R •.
- : meaning (sg X A B) R (Σ A' B')
     <- ({x} meaning B (R ; X ~> x) (B' x))
     <- meaning A R A'.
- : meaning (pair M N) R (M' , N')
     <- meaning N R N'
     <- meaning M R M'.
- : meaning (fst M) R M1
     <- meaning M R M'
     <- π₁ M' ⇒ M1.
- : meaning (snd M) R M2
     <- meaning M R M'
     <- π₂ M' ⇒ M2.
- : meaning (pi X A B) R (∏ A' B')
     <- ({x} meaning B (R ; X ~> x) (B' x))
     <- meaning A R A'.
- : meaning (lam X E) R (λ E')
     <- ({x} meaning E (R ; X ~> x) (E' x)).
- : meaning (ap M N) R MN
     <- meaning N R N'
     <- meaning M R M'
     <- M' $ N' ⇒ MN.
- : meaning (var V) R M
     <- lookup R V M.

%worlds (var-sem) (meaning _ _ _).
%covers meaning +M +R -E.